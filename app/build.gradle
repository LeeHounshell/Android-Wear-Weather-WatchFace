apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply from: '../preprocessor.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    ext.addDependency = {
        task, flavor, dependency ->
            println('task=' + (String) task + 'flavor=' + (String) flavor + 'dependency=' + (String) dependency)
    }

    if (project.hasProperty("MyProject.properties")
            && new File(project.property("MyProject.properties") as String).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("MyProject.properties"))))

        signingConfigs {
            release {
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
            debug {
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
    }

    defaultConfig {
        applicationId "com.harlie.android.sunshine.app"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            debuggable true
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'OPEN_WEATHER_MAP_API_KEY', MyOpenWeatherMapApiKey
        it.buildConfigField 'String', 'GOOGLE_PROJECT_NUMBER', MyGoogleProjectNumber
    }

    productFlavors {
        paid {
            applicationId "com.harlie.android.sunshine.app.paid"
            versionName "1.0-Paid"
        }
        free {
            applicationId "com.harlie.android.sunshine.app.free"
            versionName "1.0-Free"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.bumptech.glide:glide:3.5.2'
    compile 'com.android.support:support-v4:24.0.0-alpha2'
    compile 'com.android.support:support-annotations:24.0.0-alpha2'
    compile 'com.android.support:gridlayout-v7:24.0.0-alpha2'
    compile 'com.android.support:cardview-v7:24.0.0-alpha2'
    compile 'com.android.support:appcompat-v7:24.0.0-alpha2'
    compile 'com.android.support:design:24.0.0-alpha2'
    compile 'com.android.support:recyclerview-v7:24.0.0-alpha2'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.android.gms:play-services-wearable:8.3.0'
}
